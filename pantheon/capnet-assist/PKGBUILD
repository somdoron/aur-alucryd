# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=capnet-assist
pkgver=0.2.0.1+r99
pkgrel=1
pkgdesc='Captive Portal Assistant'
arch=('i686' 'x86_64')
url='https://launchpad.net/capnet-assist'
license=('GPL3')
groups=('pantheon')
depends=('gcr' 'glib2' 'glibc' 'gtk3' 'libsoup' 'networkmanager' 'webkit2gtk')
makedepends=('cmake' 'intltool' 'vala')
source=("capnet-assist::bzr+lp:capnet-assist/loki")
sha256sums=('SKIP')

pkgver() {
  cd capnet-assist

  echo "0.2.0.1+r$(bzr revno)"
}

prepare() {
  cd capnet-assist

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd capnet-assist/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr/'
  make
}

package() {
  cd capnet-assist/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
