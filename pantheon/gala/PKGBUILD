# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=gala
pkgver=0.3.0+r542
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=('i686' 'x86_64')
url='https://launchpad.net/gala'
license=('GPL3')
groups=('pantheon')
depends=('clutter-gtk' 'mutter' 'plank'
         'libgranite.so')
makedepends=('bzr' 'gnome-common' 'granite' 'intltool' 'vala')
provides=('gala' 'libgala.so')
conflicts=('gala')
install='gala.install'
source=('gala::bzr+lp:gala#revision=542')
sha256sums=('SKIP')

pkgver() {
  cd gala

  echo "0.3.0+r$(bzr revno)"
}

build() {
  cd gala

  ./autogen.sh \
    --prefix='/usr' \
    --disable-schemas-compile
  make
}

package() {
  cd gala

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
