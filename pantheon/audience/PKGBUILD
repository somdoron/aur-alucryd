# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=audience
pkgver=0.2.0.2+r655
pkgrel=1
pkgdesc='The Pantheon Video Player'
arch=('i686' 'x86_64')
url='https://launchpad.net/audience'
license=('GPL3')
groups=('pantheon')
depends=('clutter-gtk' 'desktop-file-utils' 'gst-plugins-base-libs'
         'libgranite.so')
makedepends=('bzr' 'clutter-gst' 'cmake' 'granite' 'intltool' 'vala')
provides=('audience')
conflicts=('audience')
source=('audience::bzr+lp:audience/0.4-loki' 'audience.patch')
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd audience

  echo "0.2.0.2+r$(bzr revno)"
}

prepare() {
  cd audience

  patch -Np1 -i ../audience.patch
}

build() {
  cd audience

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd audience/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
