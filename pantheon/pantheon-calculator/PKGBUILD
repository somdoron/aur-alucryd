# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-calculator
pkgver=0.1.1.1+r282
pkgrel=1
pkgdesc='The Pantheon Calculator'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-calculator'
license=('GPL3')
groups=('pantheon')
depends=('glib2' 'glibc' 'gtk3'
         'libgranite.so')
makedepends=('bzr' 'cmake' 'intltool' 'vala')
install="pantheon-calculator.install"
source=("pantheon-calculator::bzr+lp:pantheon-calculator/0.4-loki")
sha256sums=('SKIP')

pkgver() {
  cd pantheon-calculator

  echo "0.1.1.1+r$(bzr revno)"
}

prepare() {
  cd pantheon-calculator

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd pantheon-calculator/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd pantheon-calculator/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
