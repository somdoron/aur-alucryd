# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic@gmail.com>

pkgname=gsignond
pkgver=1.0.4+r469
pkgrel=1
pkgdesc='gSSO glib daemon'
arch=('i686' 'x86_64')
url='https://01.org/gsso'
license=('LGPL2.1')
depends=('dbus' 'sqlite')
makedepends=('git' 'gobject-introspection' 'gtk-doc')
backup=('etc/gsignond.conf')
source=('gsignond::bzr+lp:~elementary-os/gsignond/gsignond-loki')
sha256sums=('SKIP')

pkgver() {
  cd gsignond

  echo "1.0.4+r$(bzr revno)"
}

prepare() {
  cd gsignond

  mkdir -p m4
  gtkdocize
  aclocal
  autoheader
  libtoolize --copy --force
  autoconf
  automake --add-missing --copy
  autoreconf --install --force
}

build() {
  cd gsignond

  ./configure \
    --prefix='/usr' \
    --sysconfdir='/etc' \
    --enable-dbus-type='session'
  make
}

package() {
  cd gsignond

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
