# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=cerbere
pkgver=0.2.2+r52
pkgrel=1
pkgdesc='The Pantheon Watchdog'
arch=('i686' 'x86_64')
url='https://launchpad.net/cerbere'
license=('GPL2')
groups=('pantheon')
depends=('dconf' 'libgee')
makedepends=('cmake' 'vala')
install='cerbere.install'
source=("cerbere::bzr+lp:cerbere/loki")
sha256sums=('SKIP')

pkgver() {
  cd cerbere

  echo "0.2.2+r$(bzr revno)"
}

build() {
  cd cerbere

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd cerbere/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
